# The funcpy standard library. Very useful (otherwise your code won't run).
# Use it with `include standard'.

include primitives;

# basic functions
. x y v -> (x (y v));
! a -> (? a false true);
!= a b -> (! (== a b));
or a b -> (? a true (? b true false));
and a b -> (? a (? b true false) false);
flip f a b -> (f b a);
< -> (flip >);
>= a b -> (! (< a b));
<= a b -> (! (> a b));
id a -> a;
const a b -> a;

# list functions
foldr f v xs -> (? (== xs [])
  v (f (head xs) (foldr f v (tail xs)))
);
foldr1 f xs -> (? (== (tail xs) [])
  (head xs) (f (head xs) (foldr1 f (tail xs)))
);
foldl f v xs -> (? (== xs [])
  v (foldl f (f v (head xs)) (tail xs))
);
foldl1 f xs -> (foldl f (head xs) (tail xs));
++ xs ys -> (foldr : ys xs);
reverse -> (foldl (flip :) []);
length xs -> (? (== xs [])
  0 (+ 1 (length (tail xs)))
);
map f xs -> (? (== xs [])
  [] (: (f (head xs)) (map f (tail xs)))
);
filter f xs -> (? (== xs [])
  [] (? (f (head xs))
    (: (head xs) (filter f (tail xs))) (filter f (tail xs))
  )
);
fst -> head;
snd -> (. head tail);
last -> (foldr1 (flip const));
zip xs ys -> (? (or (== xs []) (== ys []))
  [] (: [(head xs), (head ys)] (zip (tail xs) (tail ys)))
);
repeat x -> (: x (repeat x));
take n xs -> (? (== n 0)
  [] (: (head xs) (take (- n 1) (tail xs)))
);
!! xs n -> (? (== n 0) (head xs) (!! (tail xs) (- n 1)));

# other functions
min a b -> (? (< a b) a b);
max a b -> (? (> a b) a b);
.. a b -> (? (== a b)
  [] (: a (range (+ a 1) b))
);
